apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "me.shouheng.letscorp"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        // enable multi dex
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // signing
    signingConfigs {
        release {}
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true // minify
            shrinkResources true // enable shrink resources
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        // config released apk name
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = defaultConfig.applicationId + "-" + variant.baseName + "-" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk";
            }
        }
    }
    // config release types
    flavorDimensions "default"
    productFlavors {
        dev {
            dimension "default"
            buildConfigField "String", "HOST", "\"fapiao-dev.glority.cn\""
        }
        stage {
            dimension "default"
            buildConfigField "String", "HOST", "\"fapiao-stg.glority.cn\""
        }
        prod {
            dimension "default"
            buildConfigField "String", "HOST", "\"fapiao.glority.cn\""
        }
    }
    // use data binding
    dataBinding {
        enabled = true
    }
    // use java 8 language
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // use external libs folder
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // support
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    // dagger
    implementation "com.google.dagger:dagger:$dagger"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger"
    // architecture components
    implementation "android.arch.lifecycle:runtime:$archVersion"
    implementation "android.arch.lifecycle:extensions:$archVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archVersion"
    implementation "android.arch.persistence.room:runtime:$archVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$archVersion"
    // use fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true
    }
    // app debug tools
    implementation 'com.facebook.stetho:stetho:1.5.0'

    implementation 'com.squareup.okhttp3:okhttp:3.3.1'
    implementation 'org.jsoup:jsoup:1.9.2'
    implementation 'com.google.code.gson:gson:2.7'

    // modules
    implementation project(':commons')
}
